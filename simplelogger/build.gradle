apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.0"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        setProperty('archivesBaseName', "$archivesBaseName-$versionName")
        consumerProguardFiles 'proguard-rules.pro'
    }

    sourceSets{
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
}

dependencies {
    testImplementation 'junit:junit:4.12'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
}

tasks.withType(Test) {
    testLogging {
        events "passed", "skipped", "failed"
    }
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId 'click.simone'
            artifactId 'simplelogger'
            version android.defaultConfig.versionName
            artifact "$buildDir/outputs/aar/$archivesBaseName-release.aar" 
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    if (!("unspecified".equalsIgnoreCase(it.name))){
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}
